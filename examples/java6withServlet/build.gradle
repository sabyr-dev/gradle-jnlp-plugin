buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'de.gliderpilot.gradle.jnlp:gradle-jnlp-plugin:+'
        classpath 'org.gretty:gretty:4.0.3'
    }
}

apply plugin: 'de.gliderpilot.jnlp'
apply plugin: 'war'
apply plugin: 'org.gretty'

dependencies {
    jnlp project(":examples:application")
    runtime 'org.codehaus.mojo.webstart:webstart-jnlp-servlet:1.0-6.0.02_ea_b02.2'
}

jnlp {
    jnlpParams << [codebase: '$$codebase']
    j2seParams = [version: '6.0+', 'max-heap-size': '256m']
    useVersions = true
    usePack200 = true
    mainClassName = 'org.example.Launcher'
    withXml {
        information {
            title project.name
            vendor project.group ?: project.name
        }
        security {
            'all-permissions'()
        }
    }
    signJarParams = [keystore: '../keystore.ks', alias: 'myalias', storepass: 'mystorepass']
}

generateJnlp << {
    jnlpFile.with {
        text = readLines('UTF-8').findAll { !it.contains('jnlp.versionEnabled') && !it.contains('jnlp.packEnabled') }.join('\n')
    }
}

war {
    from createWebstartDir
}
